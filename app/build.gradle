/*
 * SPDX-License-Identifier: GPL-3.0-only
 */
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    defaultConfig {
        namespace 'com.tiefensuche.soundcrowd'
        applicationId 'com.tiefensuche.soundcrowd'
        versionName "3.6.1"
        compileSdk 34
        minSdkVersion 16
        targetSdkVersion 34
        versionCode 76
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix '-debug'
            minifyEnabled true
        }

        release {
            debuggable false
            versionNameSuffix '-release'
            minifyEnabled true
            shrinkResources true
        }
    }
}

dependencies {
    // own modules
    implementation 'com.tiefensuche.soundcrowd:plugin:0.1'
    implementation 'com.tiefensuche.soundcrowd:waveform:1.0'

    // android support
    // material 1.12.0 requires minSdk 19
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    // exoplayer 1.3.0 requires minSdk 19
    implementation "androidx.media3:media3-exoplayer:1.2.1"

    // other third party
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    kapt 'com.github.bumptech.glide:compiler:4.14.2'
    implementation 'com.sothree.slidinguppanel:library:3.4.0'
    implementation 'com.github.AppIntro:AppIntro:6.2.0'
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'
    implementation 'com.github.myinnos:AlphabetIndex-Fast-Scroll-RecyclerView:1.0.95'
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url = uri("https://maven.pkg.github.com/soundcrowd/soundcrowd-plugin")
        credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
    maven {
        url = uri("https://maven.pkg.github.com/soundcrowd/soundcrowd-waveform")
        credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}
