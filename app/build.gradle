/*
 * SPDX-License-Identifier: GPL-3.0-only
 */
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    defaultConfig {
        namespace 'com.tiefensuche.soundcrowd'
        applicationId 'com.tiefensuche.soundcrowd'
        compileSdk 35
        minSdk 21
        targetSdk 35
        versionCode 80
        versionName "4.1.1"
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled = true
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix '-debug'
            minifyEnabled false
        }

        release {
            debuggable false
            versionNameSuffix '-release'
            minifyEnabled true
            shrinkResources true
        }
    }
}

dependencies {
    // own modules
    implementation 'com.github.soundcrowd:soundcrowd-plugin:v1.0.0'
    implementation 'com.github.soundcrowd:soundcrowd-waveform:v1.0'
    implementation 'com.github.0xf4b1:songrec-android:v0.1'

    // service plugins
    implementation project(':beatport')
    implementation project(':soundcloud')
    implementation project(':spotify')
    implementation project(':tidal')
    implementation project(':youtube')

    // android support
    // needed for newpipe extractor in youtube plugin on older android api
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs_nio:2.1.5")

    // material 1.12.0 requires minSdk 19
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    // exoplayer 1.3.0 requires minSdk 19
    implementation "androidx.media3:media3-exoplayer:1.5.1"
    implementation "androidx.media3:media3-session:1.5.1"

    // other third party
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    kapt 'com.github.bumptech.glide:compiler:4.14.2'
    implementation 'com.github.AppIntro:AppIntro:6.3.1'
    implementation 'com.github.myinnos:AlphabetIndex-Fast-Scroll-RecyclerView:1.0.95'
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}
